PROGRAM SOCKETRD

CONST
	tag_name = 'S3:'
	
VAR
	STATUS  : INTEGER
	myfile  : FILE
	s_str   : STRING[1]
	con_str : STRING[50]
	n_byte  : INTEGER
	count   : INTEGER
	tmp_num : INTEGER
	an_pos  : ARRAY[6] OF REAL
	pos_t   : XYZWPR
	flag1   : INTEGER
	
ROUTINE RecvData2Pos(con_str:STRING)
BEGIN
  FOR count=1 TO 6 DO
    tmp_num = INDEX(con_str,',')
    CNV_STR_REAL(SUB_STR(con_str,1,tmp_num-1),an_pos[count])
    WRITE('an_pos[count]=' ,an_pos[count],CR)
  	con_str = SUB_STR(con_str,tmp_num+1,50) 	
  ENDFOR
  pos_t.X = an_pos[1]
  pos_t.Y = an_pos[2]
  pos_t.Z = an_pos[3]
  pos_t.W = an_pos[4]
  pos_t.P = an_pos[5]
  pos_t.R = an_pos[6]                                     
  SET_POS_REG(10, pos_t, status)
END RecvData2Pos

BEGIN
	flag1 = 0
	SET_FILE_ATR(myfile,ATR_IA)
	SET_FILE_ATR(myfile,ATR_READAHD,2)--256BYTE
	OPEN FILE myfile('RW',tag_name)
	status = IO_STATUS(myfile)
	IF (status <> 0) THEN
		CLOSE FILE myfile
		CLR_IO_STAT(myfile)
		OPEN FILE myfile('RW',tag_name)
	ENDIF

	REPEAT
		BYTES_AHEAD(myfile,n_byte,STATUS)
		IF (UNINIT(n_byte))THEN
			 n_byte = 0
		ENDIF
		IF ((n_byte > 0) AND (status = 0))THEN
			con_str = ''
			REPEAT
				s_str = ''
				READ myfile(s_str::1)
				n_byte = n_byte - 1
				con_str = con_str + s_str
				
			UNTIL(n_byte = 0)
			RecvData2Pos(con_str)
			flag1 = 5
		ENDIF
	DELAY 100
	UNTIL((n_byte = 0) AND(flag1 = 5))
	CLOSE FILE myfile
END SOCKETRD